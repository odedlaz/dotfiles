#!/usr/bin/env fish

set DIR (dirname (readlink -f (status -f)))

# poor mans optarg :)
set rm_flags
for item in $argv
    switch "$item"
        case -f --force
           set rm_flags $rm_flags "-f"
         case -i --interactive
            set rm_flags $rm_flags "-i"
    end
end

function link
	set from $argv[1]
	set to $argv[2]

	# if $from is a file -> link it
	# if $from is a dir, and $to doesn't exist -> link it
	# otherwise, walk $from

   if test -d $from
      if test -e $to; and not test -d $to
         set_color 'red'
         echo "'$from' is a directory, '$to' is not: "(file $to)
         set_color 'normal'
         return 1
      end

      if not test -d $to
         mkdir -p $to
      end
   end

	if test -f $from; or test -d $from;and not test -d $to
      if test (count $rm_flags) -gt 0
         set_color 'yellow'
         rm -v $rm_flags $to
         set_color 'normal'
      end

      if test -e $to
         set_color 'red'
         echo "can't link '$from' -> '$to': destination already exists"
         set_color 'normal'
         return 1
      end

      ln -v -s $from $to
      return $status

	end

	for path in $from/*
		link $path $to/(basename $path)
	end
end


link "$DIR/fish" "$HOME/.config/fish"
link "$DIR/oh-my-fish" "$HOME/.config/omf"

link "$DIR/neovim/init.vim" "$HOME/.vimrc"
link "$DIR/neovim" "$HOME/.config/nvim"

link "$DIR/tmux" "$HOME/.tmux"
link "$DIR/tmux/tmux.conf" "$HOME/.tmux.conf"

link "$DIR/redshift.conf" "$HOME/.config/redshift.conf"
